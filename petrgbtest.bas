0 REM THIS ODD ROUTINE EXISTS CUZ I CAN'T TOTALLY FIGURE OUT THE CASSETTE PORT
1 PRINT "{CLEAR}{DOWN:2} INITIALIZE CASSETTE PORT #2? (Y/(N))"
2 FOR I=10 TO 1 STEP -1
3 PRINT I;"...";
4 FOR J=1 TO 500
5 GET C$ : IF C$<>"" THEN GOTO 8
6 NEXT J
7 NEXT I
8 IF C$="Y" THEN GOTO 10
9 GOTO 17
10 PRINT
11 PRINT " PLEASE WAIT ONE MOMENT...";
12 OPEN 1,2,1
13 PRINT "ONE MORE SEC..."
14 CLOSE 1
15 PRINT " OK"
16 GOTO 17
17 POKE 59459,255 :REM SET ALL BITS TO OUTPUT
18 POKE 59467,PEEK(59467) AND 227 :REM DISABL SHFTREG CB2,&SET TO OUTPUT
19 POKE 59468,(PEEK(59468) AND 31) OR 192 : REM SET CB2 LOW
20 POKE 59468,PEEK(59468) AND 254 :REM CA1 TRIGGERS FALLING EDGE
21 BB=0
22 GOSUB 194
23 PRINT "{CLEAR}{DOWN:2}
24 PRINT "  RGB LED CONTROL"
25 PRINT "  ---------------"
26 pRINT
27 PRINT "  SELECT MODE"
28 PRINT
29 PRINT "  1 ... SOLID COLOR "
30 PRINT "  2 ... RAINBOW      "
31 PRINT "  3 ... BLINKENLIGHTS "
32 PRINT "  4 ... MATRIX"
33 PRINT "  5 ... SHIFT REGISTER"
34 PRINT "  X ... EXIT LED SUBROUTINE"
35 PRINT "  Q ... QUIT TO BASIC"
36 PRINT "  A ... POWER CYCLE ARDUINO"
37 PRINT
38 PRINT
39 INPUT "  ENTER SELECTION:";C$
40 PRINT
41 IF C$="" THEN GOTO 23
42 IF C$="Q" THEN 215
43 IF C$="X" THEN C$="255"
44 IF C$="A" THEN GOSUB 194
45 BB=VAL(C$):REM CONVERTS STRING TO INTEGER, BB=0 IF NONNUMERIC
46 IF BB=0 THEN GOTO 23
47 IF BB=255 THEN 203
48 GOSUB 208 :REM SEND MODE SELECTION TO ARDUINO
49 ON BB GOTO 51, 59, 63, 71, 99
50 GOTO 23
51 PRINT "{CLEAR}{DOWN:2}"
52 PRINT " SOLID COLOR MODE"
53 INPUT " ENTER R,G,B VALUES (0-255)";R,G,B
54 BB=R : GOSUB 208
55 BB=G : GOSUB 208
56 BB=B : GOSUB 208
57 PRINT
58 GOTO 34
59 PRINT "{CLEAR}{DOWN:2}"
60 PRINT " RAINBOW MODE"
61 PRINT
62 GOTO 34
63 PRINT "{CLEAR}{DOWN:2}"
64 PRINT " BLINKENLIGHTS MODE"
65 INPUT " ENTER R,G,B VALUES (0-255)";R,G,B
66 BB=R : GOSUB 208
67 BB=G : GOSUB 208
68 BB=B : GOSUB 208
69 PRINT
70 GOTO 34
71 REM MAX # STREAMS,80/40 COLS,UPWARD SCROLL SPEED, SCREEN MEM LOC
72 M=4 :C=80 :S$="    ":L=32768
73 FOR I=0 TO 24 : PRINT : NEXT I: REM CLEAR SCREEN
74 GET A$: IF A$<>"" THEN GOTO 96 :REM EXIT IF KEYPRESS
75 GOSUB 78
76 IF RND(1)<.38 THEN GOSUB 87 :REM LIKELYHOOD OF DRIP 38%
77 GOTO 74
78 REM        CHARACTER STREAMS
79 S=RND(1)*M : REM NUMBER OF STREAMS IN THIS LOOP
80 FOR I=0 TO S :X(I)=INT(RND(1)*C) :NEXT I :REM STREAM POSITIONS
81 REM DEFINE STARTING&ENDING LOCATIONS FOR STREAM, & SPEED
82 FOR Y=RND(1)*15 TO RND(1)*7+18 STEP RND(1)+.3
83 FOR I=0 TO S : REM DRAW STREAMS
84 POKE L+X(I)+(INT(Y-I)*C),RND(1)*255
85 PRINT S$; :REM SCROLL UPWARDS
86 NEXT I :NEXT Y :RETURN
87 REM        CHARACTER DRIPS
88 REM NUMBER OF POSSIBLE DRIPS MAX STREAMS-1
89 FOR I=1 TO RND(1)*(M-1)
90 DX=RND(1)*C :REM DRIP X POSITION
91 FOR Y=1 TO 24 STEP RND(1)+.75 :REM DRAW DRIP
92 PY=L+DX+INT(Y)*C
93 POKE PY,RND(1)*255 :POKE PY,RND(1)*255 :POKE PY,32
94 PRINT S$; : REM SCROLL UP
95 NEXT Y :NEXT I :RETURN
96 BB=255 : GOSUB 208
97 FOR I=0 TO 24 : PRINT : NEXT I
98 GOTO 23
99 PRINT "{CLEAR}{DOWN:2}"
100 PRINT " SHIFT REGISTER MODE"
101 PRINT
102 INPUT " ENTER R,G,B VALUES (0-255)";R,G,B
103 BB=R : GOSUB 208
104 BB=G : GOSUB 208
105 BB=B : GOSUB 208
106 INPUT " ENTER LED MATRIX REFRESH DELAY (0-255)";BB
107 GOSUB 208
108 PRINT
109 PRINT " PRESS M TO MUTE AUDIO"
110 PRINT " PRESS ANY OTHER KEY TO CONTINUE"""
111 BB=1
112 GET A$
113 IF A$="" THEN GOTO  112
114 IF A$="M" THEN BB=0
115 GOSUB 208
116 POKE 59467,PEEK(59467)AND227OR16 :REM ENABLE SHIFT REG
117 T=0
118 D=0
119 P$="+"
120 REM SET UP FOR DECIMAL TO BINARY CALCULATIONS
121 Z(1)=1
122 FOR I=2 TO 8
123 Z(I)=Z(I-1)*2
124 NEXT I
125 POKE 59464,T : REM SETS SHIFT REGISTER TIMER
126 POKE 59466,D : REM OUPUTS ON SHIFT REGISTER
127 PRINT "{CLEAR}{DOWN:2} SHIFT REGISTER MODE"
128 PRINT
129 PRINT " TIMER:";T
130 PRINT
131 PRINT " DATA:";
132 REM CALCULATE AND DISPLAY WHICH BITS ARE ON
133 PRINT (DAND1);P$;(DAND2);P$;(DAND4);P$;(DAND8);P$;
134 PRINT (DAND16);P$;(DAND32);P$;(DAND64);P$;(DAND128);"=";D
135 PRINT
136 PRINT " A - DECREASE DATA-10        E - DECREASE TIMER-10"
137 PRINT " S - DECREASE DATA-1         R - DECREASE TIMER-1"
138 PRINT " D - SET DATA TO 170         T - SET TIMER TO 128"
139 PRINT " F - INCREASE DATA+1         Y - INCREASE TIMER+1"
140 PRINT " G - INCREASE DATA+10        U - INCREASE TIMER+10"
141 PRINT " X - EXIT SUBROUTINE         Q - QUIT TO BASIC"
142 PRINT " 0 - SET TIMER & DATA TO 0   C - CYCLE AUTOMATICALLY"
143 PRINT
144 PRINT " NUMBERS 1 THROUGH 8 SET CORRESPONDING BITS ON DATA STREAM"
145 GET K$
146 IF K$="" THEN 145
147 K=VAL(K$)
148 IF K=0 THEN GOTO 152
149 K=Z(K)
150 D=(NOT(D AND K)) AND (NOT(NOT D AND NOT K)):REM XOR BITS
151 GOTO 125
152 IF K$="E" THEN T=T-10
153 IF K$="R" THEN T=T-1
154 IF K$="T" THEN GOTO 170
155 IF K$="Y" THEN T=T+1
156 IF K$="U" THEN T=T+10
157 IF K$="A" THEN D=D-10
158 IF K$="S" THEN D=D-1
159 IF K$="D" THEN GOTO 180
160 IF K$="F" THEN D=D+1
161 IF K$="G" THEN D=D+10
162 IF K$="0" THEN D=0:T=0
163 IF K$="X" THEN GOTO 17
164 IF K$="Q" THEN GOTO 190
165 IF D<=-1 THEN D=255
166 IF D>=256 THEN D=0
167 IF T<=-1 THEN T=255
168 IF T>=256 THEN T=0
169 GOTO 125
170 FOR T=0 TO 255
171 POKE 59464,T : REM SETS SHIFT REGISTER TIMER
172 PRINT "{CLEAR}{DOWN:2} SHIFT REGISTER MODE"
173 PRINT
174 PRINT " TIMER:";T;"  DATA:";D
175 FOR I=1 TO 50
176 GET K$ : IF K$<>"" THEN GOTO 125
177 NEXT I
178 NEXT T
179 GOTO 170
180 FOR D=0 TO 255
181 POKE 59466,D : REM OUPUTS ON SHIFT REGISTER
182 PRINT "{CLEAR}{DOWN:2} SHIFT REGISTER MODE"
183 PRINT
184 PRINT " TIMER:";T;"  DATA:";D
185 FOR I=1 TO 50
186 GET K$ : IF K$<>"" THEN GOTO 125
187 NEXT I
188 NEXT D
189 GOTO 180
190 PRINT
191 PRINT " QUITING TO BASIC"
192 PRINT " SHIFT REGISTER MODE IS ACTIVE"
193 END
194 PRINT "{CLEAR}{DOWN:2}POWER CYCLING ARDUINO"
195 POKE 59456,(PEEK(59456)OR16) :REM CASSETTE PORT 2 MOTOR OFF
196 PRINT
197 PRINT "ONE MOMENT";
198 FOR J=1 TO 100 :PRINT ".";:NEXT J
199 POKE 59456,(PEEK(59456)AND239):REM CASSETTE PORT 2 MOTOR ON
200 FOR J=1 TO 600 :PRINT ".";:NEXT J
201 PRINT : PRINT "OK"
202 RETURN
203 PRINT
204 PRINT " EXITING SUBROUTINE..."
205 GOSUB 208
206 PRINT " OK"
207 GOTO 23
208 REM SEND BB BYTE
209 POKE 59468,PEEK(59468) OR 224 :REM SET CB2 HIGH TO SIGNAL ARDUINO
210 POKE 59457,BB :REM SEND BYTE
211 POKE 59468,(PEEK(59468) AND 31) OR 192 :REM RETURN CB2 TO LOW STATE
212 REM PART OF SENDBYTE SUBROUTINE, BUT DOESN'T STRICTLY HAVE TO BE
213 IF (PEEK(59469)AND2)=0 THEN 212 : REM WAIT FOR CA1 STROBE FROM ARDUINO
214 RETURN
215 PRINT
216 PRINT "EXITING RGB CONTROL"
217 END
218 REM REMINDERS
219 POKE 59468,PEEK(59468) OR 224 :REM SET CB2 HIGH
220 POKE 59468,(PEEK(59468) AND 31) OR 192 :END :REM SET CB2 LOW
221 POKE 59459,255 :REM SET ALL BITS TO OUTPUT
222 POKE 59467,PEEK(59467) AND 227 :REM DISABL SHFTREG CB2,&SET TO OUTPUT
223 POKE 59467,PEEK(59467)AND227OR16 :REM ENABLE SHIFT REG
224 POKE 59464,T : REM SETS SHIFT REGISTER TIMER
225 POKE 59466,SHFTDATA: REM OUPUTS ON SHIFT REGISTER
226 POKE 59471,J :REM SEND DATA OUT
227 POKE 59457,J :REM SEND DATA OUT AND RESET CA1 FLAG
228 POKE 59468,PEEK(59468) AND 254 :REM CA1 TRIGGERS FALLING EDGE
229 POKE 59468,PEEK(59468) OR 1 :REM CA1 TRGGERS RISING EDGE
230 IF (PEEK(59469) AND 2)=0 THEN WHATEVER :REM WAIT FOR CA1 TO GO HIGH
231 POKE 59467,PEEK(59467) OR 1 :REM DATA LATCHED WHEN CA1 TRIGGERS
232 POKE 59468,PEEK(59468) OR 224 :REM SET CB2 HIGH
233 POKE 59456,(PEEK(59456)OR16) :REM TURN OFF CASSETTE 2 MOTOR/ARDUINO
234 POKE 59456,(PEEK(59456)AND239):REM TURN ON CASSETTE 2 MOTOR/ARDUINO
