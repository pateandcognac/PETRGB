0 REM THIS ODD ROUTINE EXISTS CUZ I CAN'T TOTALLY FIGURE OUT THE CASSETTE PORT
1 PRINT "{CLEAR}{DOWN:2} INITIALIZE CASSETTE PORT #2? (Y/(N))"
2 GET C$
3 IF C$="" THEN GOTO 2
4 IF C$="Y" THEN GOTO 6
5 GOTO 12
6 PRINT
7 PRINT "PRESS RUN/STOP, AND THEN TYPE 'CONT' AND PRESS <RETURN>"
8 LOAD "",2
9 PRINT
10 PRINT " OK"
11 GOTO 12
12 POKE 59459,255 :REM SET ALL BITS TO OUTPUT
13 POKE 59467,PEEK(59467) AND 227 :REM DISABL SHFTREG CB2,&SET TO OUTPUT
14 POKE 59468,(PEEK(59468) AND 31) OR 192 : REM SET CB2 LOW
15 POKE 59468,PEEK(59468) AND 254 :REM CA1 TRIGGERS FALLING EDGE
16 BB=0
17 GOSUB 169
18 PRINT "{CLEAR}{DOWN:2}
19 PRINT "  RGB LED CONTROL"
20 PRINT "  ---------------"
21 pRINT
22 PRINT "  SELECT MODE"
23 PRINT
24 PRINT "  1 ... SOLID COLOR "
25 PRINT "  2 ... RAINBOW      "
26 PRINT "  3 ... BLINKENLIGHTS "
27 PRINT "  4 ... MATRIX"
28 PRINT "  5 ... SHIFT REGISTER"
29 PRINT "  X ... EXIT LED SUBROUTINE"
30 PRINT "  Q ... QUIT TO BASIC"
31 PRINT "  A ... POWER CYCLE ARDUINO"
32 PRINT
33 PRINT
34 INPUT "  ENTER SELECTION:";C$
35 PRINT
36 IF C$="" THEN GOTO 18
37 IF C$="Q" THEN 190
38 IF C$="X" THEN C$="255"
39 IF C$="A" THEN GOSUB 169
40 BB=VAL(C$):REM CONVERTS STRING TO INTEGER, BB=0 IF NONNUMERIC
41 IF BB=0 THEN GOTO 18
42 IF BB=255 THEN 178
43 GOSUB 183 :REM SEND MODE SELECTION TO ARDUINO
44 ON BB GOTO 46, 54, 58, 66, 94
45 GOTO 18
46 PRINT "{CLEAR}{DOWN:2}"
47 PRINT " SOLID COLOR MODE"
48 INPUT " ENTER R,G,B VALUES (0-255)";R,G,B
49 BB=R : GOSUB 183
50 BB=G : GOSUB 183
51 BB=B : GOSUB 183
52 PRINT
53 GOTO 29
54 PRINT "{CLEAR}{DOWN:2}"
55 PRINT " RAINBOW MODE"
56 PRINT
57 GOTO 29
58 PRINT "{CLEAR}{DOWN:2}"
59 PRINT " BLINKENLIGHTS MODE"
60 INPUT " ENTER R,G,B VALUES (0-255)";R,G,B
61 BB=R : GOSUB 183
62 BB=G : GOSUB 183
63 BB=B : GOSUB 183
64 PRINT
65 GOTO 29
66 REM MAX # STREAMS,80/40 COLS,UPWARD SCROLL SPEED, SCREEN MEM LOC
67 M=4 :C=80 :S$="    ":L=32768
68 FOR I=0 TO 24 : PRINT : NEXT I: REM CLEAR SCREEN
69 GET A$: IF A$<>"" THEN GOTO 91 :REM EXIT IF KEYPRESS
70 GOSUB 73
71 IF RND(1)<.38 THEN GOSUB 82 :REM LIKELYHOOD OF DRIP 38%
72 GOTO 69
73 REM        CHARACTER STREAMS
74 S=RND(1)*M : REM NUMBER OF STREAMS IN THIS LOOP
75 FOR I=0 TO S :X(I)=INT(RND(1)*C) :NEXT I :REM STREAM POSITIONS
76 REM DEFINE STARTING&ENDING LOCATIONS FOR STREAM, & SPEED
77 FOR Y=RND(1)*15 TO RND(1)*7+18 STEP RND(1)+.3
78 FOR I=0 TO S : REM DRAW STREAMS
79 POKE L+X(I)+(INT(Y-I)*C),RND(1)*255
80 PRINT S$; :REM SCROLL UPWARDS
81 NEXT I :NEXT Y :RETURN
82 REM        CHARACTER DRIPS
83 REM NUMBER OF POSSIBLE DRIPS MAX STREAMS-1
84 FOR I=1 TO RND(1)*(M-1)
85 DX=RND(1)*C :REM DRIP X POSITION
86 FOR Y=1 TO 24 STEP RND(1)+.75 :REM DRAW DRIP
87 PY=L+DX+INT(Y)*C
88 POKE PY,RND(1)*255 :POKE PY,RND(1)*255 :POKE PY,32
89 PRINT S$; : REM SCROLL UP
90 NEXT Y :NEXT I :RETURN
91 BB=255 : GOSUB 183
92 FOR I=0 TO 24 : PRINT : NEXT I
93 GOTO 18
94 PRINT "{CLEAR}{DOWN:2}"
95 PRINT " SHIFT REGISTER MODE"
96 PRINT
97 INPUT " ENTER R,G,B VALUES (0-255)";R,G,B
98 BB=R : GOSUB 183
99 BB=G : GOSUB 183
100 BB=B : GOSUB 183
101 INPUT " ENTER LED MATRIX REFRESH DELAY (0-255)";BB
102 GOSUB 183
103 PRINT
104 PRINT " PRESS M TO MUTE AUDIO"
105 PRINT " PRESS ANY OTHER KEY TO CONTINUE"""
106 BB=1
107 GET A$
108 IF A$="" THEN GOTO  107
109 IF A$="M" THEN BB=0
110 GOSUB 183
111 POKE 59467,PEEK(59467)AND227OR16 :REM ENABLE SHIFT REG
112 T=0
113 D=0
114 P$="+"
115 REM SET UP FOR DECIMAL TO BINARY CALCULATIONS
116 Z(1)=1
117 FOR I=2 TO 8
118 Z(I)=Z(I-1)*2
119 NEXT I
120 POKE 59464,T : REM SETS SHIFT REGISTER TIMER
121 POKE 59466,D : REM OUPUTS ON SHIFT REGISTER
122 PRINT "{CLEAR}{DOWN:2} SHIFT REGISTER MODE"
123 PRINT
124 PRINT " TIMER:";T
125 PRINT
126 PRINT " DATA:";
127 REM CALCULATE AND DISPLAY WHICH BITS ARE ON
128 PRINT (DAND1);P$;(DAND2);P$;(DAND4);P$;(DAND8);P$;
129 PRINT (DAND16);P$;(DAND32);P$;(DAND64);P$;(DAND128);"=";D
130 PRINT
131 PRINT " A - DECREASE DATA-10         E - DECREASE TIMER-10"
132 PRINT " S - DECREASE DATA-1          R - DECREASE TIMER-1"
133 PRINT " D - SET DATA TO 170          T - SET TIMER TO 128"
134 PRINT " F - INCREASE DATA+1          Y - INCREASE TIMER+1"
135 PRINT " G - INCREASE DATA+10         U - INCREASE TIMER+10"
136 PRINT " X - EXIT SUBROUTINE          Q - QUIT TO BASIC"
137 PRINT " 0 - SET TIMER & DATA TO 0
138 PRINT
139 PRINT " NUMBERS 1 THROUGH 8 SET CORRESPONDING BITS ON DATA STREAM"
140 GET K$
141 IF K$="" THEN 140
142 K=VAL(K$)
143 IF K=0 THEN GOTO 147
144 K=Z(K)
145 D=(NOT(D AND K)) AND (NOT(NOT D AND NOT K)):REM XOR BITS
146 GOTO 120
147 IF K$="E" THEN T=T-10
148 IF K$="R" THEN T=T-1
149 IF K$="T" THEN T=128
150 IF K$="Y" THEN T=T+1
151 IF K$="U" THEN T=T+10
152 IF K$="A" THEN D=D-10
153 IF K$="S" THEN D=D-1
154 IF K$="D" THEN D=170
155 IF K$="F" THEN D=D+1
156 IF K$="G" THEN D=D+10
157 IF K$="0" THEN D=0:T=0
158 IF K$="X" THEN GOTO 12
159 IF K$="Q" THEN GOTO 165
160 IF D<=-1 THEN D=255
161 IF D>=256 THEN D=0
162 IF T<=-1 THEN T=255
163 IF T>=256 THEN T=0
164 GOTO 120
165 PRINT
166 PRINT " QUITING TO BASIC"
167 PRINT " SHIFT REGISTER MODE IS ACTIVE"
168 END
169 POKE 59456,(PEEK(59456)OR16) :REM CASSETTE PORT 2 MOTOR OFF
170 PRINT "{CLEAR}{DOWN:2}POWER CYCLING ARDUINO"
171 PRINT
172 PRINT "ONE MOMENT";
173 FOR J=1 TO 25 :PRINT ".";:NEXT J
174 POKE 59456,(PEEK(59456)AND239):REM CASSETTE PORT 2 MOTOR ON
175 FOR J=1 TO 500 :PRINT ".";:NEXT J
176 PRINT : PRINT "OK"
177 RETURN
178 PRINT
179 PRINT " EXITING SUBROUTINE..."
180 GOSUB 183
181 PRINT " OK"
182 GOTO 18
183 REM SEND BB BYTE
184 POKE 59468,PEEK(59468) OR 224 :REM SET CB2 HIGH TO SIGNAL ARDUINO
185 POKE 59457,BB :REM SEND BYTE
186 POKE 59468,(PEEK(59468) AND 31) OR 192 :REM RETURN CB2 TO LOW STATE
187 REM PART OF SENDBYTE SUBROUTINE, BUT DOESN'T STRICTLY HAVE TO BE
188 IF (PEEK(59469)AND2)=0 THEN 187 : REM WAIT FOR CA1 STROBE FROM ARDUINO
189 RETURN
190 PRINT
191 PRINT "EXITING RGB CONTROL"
192 END
193 REM REMINDERS
194 POKE 59468,PEEK(59468) OR 224 :REM SET CB2 HIGH
195 POKE 59468,(PEEK(59468) AND 31) OR 192 :END :REM SET CB2 LOW
196 POKE 59459,255 :REM SET ALL BITS TO OUTPUT
197 POKE 59467,PEEK(59467) AND 227 :REM DISABL SHFTREG CB2,&SET TO OUTPUT
198 POKE 59467,PEEK(59467)AND227OR16 :REM ENABLE SHIFT REG
199 POKE 59464,T : REM SETS SHIFT REGISTER TIMER
200 POKE 59466,SHFTDATA: REM OUPUTS ON SHIFT REGISTER
201 POKE 59471,J :REM SEND DATA OUT
202 POKE 59457,J :REM SEND DATA OUT AND RESET CA1 FLAG
203 POKE 59468,PEEK(59468) AND 254 :REM CA1 TRIGGERS FALLING EDGE
204 POKE 59468,PEEK(59468) OR 1 :REM CA1 TRGGERS RISING EDGE
205 IF (PEEK(59469) AND 2)=0 THEN WHATEVER :REM WAIT FOR CA1 TO GO HIGH
206 POKE 59467,PEEK(59467) OR 1 :REM DATA LATCHED WHEN CA1 TRIGGERS
207 POKE 59468,PEEK(59468) OR 224 :REM SET CB2 HIGH
208 POKE 59456,(PEEK(59456)OR16) :REM TURN OFF CASSETTE 2 MOTOR/ARDUINO
209 POKE 59456,(PEEK(59456)AND239):REM TURN ON CASSETTE 2 MOTOR/ARDUINO
